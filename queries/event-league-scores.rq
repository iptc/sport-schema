# Use case: Event / League / 1. What are the current scores? (Scoreboard)
# https://github.com/iptc/sport-model/wiki/Use-Cases

PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sport: <https://sportschema.org/ontologies/main/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 

SELECT (CONCAT(STR(YEAR(?date)),"-",STR(MONTH(?date)),"-",STR(DAY(?date))) as ?displayDate)
       (CONCAT(STR(HOURS(?date)),":",STR(MINUTES(?date))) as ?displayTime)
       (CONCAT(?homeTeamName," v ",?awayTeamName) as ?match) ?eventStatus
       ?homeTeamScore ?awayTeamScore
WHERE
{
    ?event rdf:type sport:Event ;
           sport:startDateTime ?date ;
           sport:eventStatus ?eventStatus ;
           sport:participation ?homeTeamParticipation ;
           sport:participation ?awayTeamParticipation ;
           sport:location ?site .
    ?homeTeamParticipation rdf:type sport:TeamParticipation ;
                           sport:participationBy ?homeTeam ;
                           sport:alignment "home" .
	OPTIONAL { ?homeTeamParticipation sport:score ?homeTeamScore . }
    ?homeTeam rdf:type sport:Team ;
              rdfs:label ?homeTeamName .
    ?awayTeamParticipation rdf:type sport:TeamParticipation ;
                           sport:participationBy ?awayTeam ;
                           sport:alignment "away" .
	OPTIONAL { ?awayTeamParticipation sport:score ?awayTeamScore . }
    ?awayTeam rdf:type sport:Team ;
              rdfs:label ?awayTeamName .
    ?site rdf:type sport:Site ;
          rdfs:label ?siteName .
    FILTER ( ?date > "2021-05-23T00:00:00Z"^^xsd:dateTime ) 
    FILTER ( ?date < "2021-05-24T00:00:00Z"^^xsd:dateTime ) 
}
ORDER BY ?week ?displayDate ?displayTime
