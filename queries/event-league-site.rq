# Use case: Event / League / 4. Where are today's matches being played?
# https://github.com/iptc/sport-model/wiki/Use-Cases

PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sport: <https://sportschema.org/ontologies/main/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 

SELECT (CONCAT(STR(YEAR(?date)),"-",STR(MONTH(?date)),"-",STR(DAY(?date))) as ?displayDate)
       (CONCAT(STR(HOURS(?date)),":",STR(MINUTES(?date))) as ?displayTime)
       (CONCAT(?homeTeamName," v ",?awayTeamName) as ?match)
       ?siteName
WHERE
{
    ?event rdf:type sport:Event ;
           sport:startDateTime ?date ;
           sport:location ?site ;
           sport:participation ?homeTeamParticipation ;
           sport:participation ?awayTeamParticipation .
    ?homeTeamParticipation rdf:type sport:TeamParticipation ;
                           sport:participationBy ?homeTeam ;
                           sport:alignment "home" .
    ?homeTeam rdf:type sport:Team ;
              rdfs:label ?homeTeamName .
    ?awayTeamParticipation rdf:type sport:TeamParticipation ;
                           sport:participationBy ?awayTeam ;
                           sport:alignment "away" .
    ?awayTeam rdf:type sport:Team ;
              rdfs:label ?awayTeamName .
    ?site rdf:type sport:Site ;
          rdfs:label ?siteName .
    FILTER ( ?date > "2014-10-04T00:00:00Z"^^xsd:dateTime ) 
    FILTER ( ?date < "2014-10-05T00:00:00Z"^^xsd:dateTime ) 
}
ORDER BY ?week ?displayDate ?displayTime
