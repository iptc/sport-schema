# Use case: Event / League / 2. What is the current status of an event (pre-, mid-, post-, postponed, suspended, canceled, etc.)
# https://github.com/iptc/sport-model/wiki/Use-Cases

PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sport: <https://sportschema.org/ontologies/main/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 

SELECT DISTINCT (CONCAT(STR(YEAR(?date)),"-",STR(MONTH(?date)),"-",STR(DAY(?date))) as ?displayDate)
       (CONCAT(STR(HOURS(?date)),":",STR(MINUTES(?date))) as ?displayTime)
       (CONCAT(?homeTeamName," v ",?awayTeamName) as ?match)
       ?status
WHERE
{
    ?event rdf:type sport:Event ;
           sport:startDateTime ?date ;
           sport:participation ?homeTeamParticipation ;
           sport:participation ?awayTeamParticipation ;
           sport:eventStatus ?status .
    ?homeTeamParticipation rdf:type sport:TeamParticipation ;
                           sport:participationBy ?homeTeam ;
                           sport:alignment "home" ;
                           sport:score ?homeTeamScore .
    ?homeTeam rdf:type sport:Team ;
              rdfs:label ?homeTeamName .
    ?awayTeamParticipation rdf:type sport:TeamParticipation ;
                           sport:participationBy ?awayTeam ;
                           sport:alignment "away" ;
                           sport:score ?awayTeamScore .
    ?awayTeam rdf:type sport:Team ;
              rdfs:label ?awayTeamName .
    FILTER ( ?date > "2021-04-25T00:00:00Z"^^xsd:dateTime ) 
    FILTER ( ?date < "2021-04-26T00:00:00Z"^^xsd:dateTime ) 
}
ORDER BY ?displayDate ?displayTime
