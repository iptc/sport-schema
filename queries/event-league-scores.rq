# Use case: Event / League / 1. What are the current scores? (Scoreboard)
# https://github.com/iptc/sport-model/wiki/Use-Cases

PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sport: <http://www.iptc.org/ontologies/Sport/>
PREFIX spstat: <http://cv.iptc.org/newscodes/spstat/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 

SELECT (CONCAT(STR(YEAR(?date)),"-",STR(MONTH(?date)),"-",STR(DAY(?date))) as ?displayDate)
       (CONCAT(STR(HOURS(?date)),":",STR(MINUTES(?date))) as ?displayTime)
       (CONCAT(?homeTeamName," v ",?awayTeamName) as ?match)
       ?homeTeamScore ?awayTeamScore
WHERE
{
    ?event rdf:type sport:Event .
    ?event sport:startDate ?date .
    ?event sport:eventParticipation ?homeTeamParticipation .
    ?homeTeamParticipation rdf:type sport:TeamParticipation .
    ?homeTeamParticipation sport:participationBy ?homeTeam .
    ?homeTeamParticipation sport:alignment "home" .
    ?homeTeamParticipation spstat:score ?homeTeamScore .
    ?homeTeam rdf:type sport:Team .
    ?homeTeam rdfs:label ?homeTeamName .
    ?event sport:eventParticipation ?awayTeamParticipation .
    ?awayTeamParticipation rdf:type sport:TeamParticipation .
    ?awayTeamParticipation sport:participationBy ?awayTeam .
    ?awayTeamParticipation sport:alignment "away" .
    ?awayTeamParticipation spstat:score ?awayTeamScore .
    ?awayTeam rdf:type sport:Team .
    ?awayTeam rdfs:label ?awayTeamName .
    ?event sport:site ?site .
    ?site rdf:type sport:Site .
    ?site rdfs:label ?siteName .
    FILTER ( ?date > "2021-02-15T00:00:00Z"^^xsd:dateTime ) 
    FILTER ( ?date < "2021-02-16T00:00:00Z"^^xsd:dateTime ) 
}
ORDER BY ?week ?displayDate ?displayTime
