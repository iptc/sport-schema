# Use case: Season / League / 1. What are the current standings?
# Use case: What is the team record broken down into home and away events?
# https://github.com/iptc/sport-model/wiki/Use-Cases

PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sport: <https://sportschema.org/ontologies/main/>
PREFIX spstat: <https://sportschema.org/ontologies/corestatistics/>
PREFIX spsocstat: <https://sportschema.org/ontologies/soccer/>
PREFIX spcompetitionscope: <http://cv.iptc.org/newscodes/spcompetitionscope/>

SELECT ?teamName ?GP ?P ?W ?L ?D ?GF ?GA ?GD ?WHOME ?LHOME ?WAWAY
WHERE
{
    ?season rdf:type sport:Competition .
    ?season sport:competitionType <http://cv.iptc.org/newscodes/spct/competition> .
    ?season sport:competitionParticipation ?teamParticipation . 
    ?teamParticipation rdf:type sport:TeamParticipation .
    ?teamParticipation sport:participationBy ?team .
    ?team rdf:type sport:Team .
    ?team rdfs:label ?teamName .
    ?teamParticipation spstat:events-played ?GP .
    ?teamParticipation spstat:standing-points ?P .
    ?teamParticipation spstat:wins ?W .
    ?teamParticipation spstat:losses ?L .
    ?teamParticipation spstat:ties ?D .
    ?teamParticipation spstat:points-scored-for ?GF .
    ?teamParticipation spstat:points-scored-against ?GA .
    ?teamParticipation spstat:points-difference ?GD .
    ?teamParticipation sport:participationSplit ?splithome .
    ?splithome rdf:type sport:ParticipationSplit .
    ?splithome sport:participationSplitType spcompetitionscope:events-home .
    ?splithome spstat:wins ?WHOME .
    ?splithome spstat:losses ?LHOME .
    ?teamParticipation sport:participationSplit ?splitaway .
    ?splitaway rdf:type sport:ParticipationSplit .
    ?splitaway sport:participationSplitType spcompetitionscope:events-away .
    ?splitaway spstat:wins ?WAWAY .
    FILTER (?season = <http://example.com/Competition/l.premierleague.com-2020>)
}
ORDER BY DESC(?P) DESC(?W) DESC(?GD)
