# Use case: Season / Team / 1. Who plays for the team? (Includes jersey number, position, etc.)
# https://github.com/iptc/sport-model/wiki/Use-Cases

PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sport: <https://sportschema.org/ontologies/main/>
PREFIX spstat: <https://sportschema.org/ontologies/corestatistics/>
PREFIX spsocstat: <https://sportschema.org/ontologies/soccer/>
PREFIX spsocposition: <http://cv.iptc.org/newscodes/spsocposition/>
PREFIX spichposition: <http://cv.iptc.org/newscodes/spichposition/>

SELECT DISTINCT ?teamName ?playerName ?playerPos ?dob ?uniformNumber
WHERE
{
    ?membership rdf:type sport:IndividualMembership ;
                sport:member ?player ;
    		    sport:membershipOf ?team .
    ?team rdf:type sport:Team ;
		  rdfs:label ?teamName .
    ?player rdf:type sport:Athlete ;
            rdfs:label ?playerName .

    OPTIONAL { ?membership sport:positionRegular ?playerPos . }
    OPTIONAL { ?membership sport:uniformNumber ?uniformNumber . }
    OPTIONAL { ?player sport:dateOfBirth ?dob . }
    # query works for either soccer or hockey
    FILTER (?team = <http://example.com/Team/l.nhl.com-t.21>)
}
ORDER BY ?teamName ?playerName
