# Use case: Event / Player / 1. Was this player in the starting lineup?
# https://github.com/iptc/sport-model/wiki/Use-Cases

PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sport: <http://www.iptc.org/ontologies/Sport/>

SELECT DISTINCT ?teamName ?playerName ?playerStatus
WHERE { 
    ?event rdf:type sport:Event ;
           sport:eventParticipation ?playerperf .
    ?playerperf sport:participationBy ?player ;
                rdf:type sport:IndividualParticipation ;
                sport:playerStatus <http://cv.iptc.org/newscodes/spperstatus/starter> ;
                sport:playerStatus ?playerStatus .
    ?player rdfs:label ?playerName ;
            rdf:type sport:Athlete .
    ?team sport:membership ?membership .
    ?membership sport:membershipBy ?player ;
                sport:membershipOf ?team .
    ?team rdf:type sport:Team ;
          rdfs:label ?teamName .
}
ORDER BY ?teamName ?playerName
